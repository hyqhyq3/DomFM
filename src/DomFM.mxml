<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" nativeDragEnter="windowedapplication1_nativeDragEnterHandler(event)"
					   nativeDragDrop="windowedapplication1_nativeDragDropHandler(event)"
					   xmlns:s="library://ns.adobe.com/flex/spark" windowComplete="windowedapplication1_windowCompleteHandler(event)"
					   xmlns:mx="library://ns.adobe.com/flex/mx" showStatusBar="false"
					   width="420" height="185" backgroundColor="#CFCFCF" initialize="windowedapplication1_initializeHandler(event)">
	<fx:Style source="style.css"/>
	<fx:Script>
		<![CDATA[
			import com.domlib.domFM.action.CurlLoader;
			import com.domlib.domFM.action.HotKey;
			import com.domlib.domFM.action.ID3Parser;
			import com.domlib.domFM.action.Player;
			import com.domlib.domFM.action.QQMusic;
			import com.domlib.domFM.data.ID3Tag;
			import com.domlib.domFM.events.ID3Event;
			import com.domlib.domFM.events.PlayEvent;
			import com.domlib.domFM.utils.FileUtil;
			import com.domlib.domFM.utils.SQL;
			import com.domlib.domFM.utils.SharedObjectTool;
			import com.domlib.domFM.utils.StringUtil;
			
			import flash.utils.setTimeout;
			
			import mx.charts.AreaChart;
			import mx.events.AIREvent;
			import mx.events.FlexEvent;
			import mx.events.FlexNativeWindowBoundsEvent;
			import mx.managers.DragManager;
			import mx.rpc.http.HTTPService;
			
			private var hotKey:HotKey;
			
			protected function windowedapplication1_initializeHandler(event:FlexEvent):void
			{
				var bytes:ByteArray = FileUtil.openAsByteArray(zipFile);
				if(bytes)
				{
					bytes.position = 0;
					dbVersion = bytes.readUnsignedInt();
				}
				var text:String = FileUtil.openAsString("config/config.xml");
				if(text)
				{
					var config:XML;
					try
					{
						config = new XML(text);
						downloadUrl = String(config.download[0]);
						syncServer = String(config.sync[0]);
					}
					catch(e:Error){}
				}
				var loader:URLLoader = new URLLoader();
				loader.addEventListener(Event.COMPLETE,onDbComp);
				loader.addEventListener(IOErrorEvent.IO_ERROR,onIoError);
				loader.dataFormat = URLLoaderDataFormat.BINARY;
				loader.load(new URLRequest(downloadUrl));
				hotKey = new HotKey();
				hotKey.addEventListener(KeyboardEvent.KEY_DOWN,onKeyDown);
				id3Parser = new ID3Parser;
				id3Parser.addEventListener(ID3Event.GET_ONE_INFO,onGetMusicInfo);
				this.nativeApplication.addEventListener(Event.EXITING,onExiting);
			}
			
			protected function onDbComp(event:Event):void
			{
				var loader:URLLoader = event.target as URLLoader;
				var bytes:ByteArray = loader.data;
				bytes.position = 0;
				var version:uint = bytes.readUnsignedInt();
				if(version>dbVersion)
				{
					dbVersion = version;
					var data:ByteArray = new ByteArray();
					bytes.readBytes(data);
					data.uncompress(CompressionAlgorithm.LZMA);
					if(sql)
					{
						sql.close();
					}
					FileUtil.save("music.db",data);
					sql = new SQL("music.db");
				}
				
			}
			
			private function onIoError(event:Event):void
			{
				
			}
			
			
			private var downloadUrl:String = "";
			private var syncServer:String = "";
			/**
			 * 应用程序退出
			 */	
			protected function onExiting(event:Event):void
			{
				if(hotKey)
				{
					hotKey.exit();
				}
				if(player)
				{
					player.exit();
				}
				if(id3Parser)
					id3Parser.exit();
				event.preventDefault();
				sendData();
			}
			
			private var dbVersion:uint = 1;
			
			private var zipFile:String = "musicdb.zip";
			
			private function sendData():void
			{
				var data:ByteArray = new ByteArray();
				var bytes:ByteArray = FileUtil.openAsByteArray("music.db");
				dbVersion++;
				data.writeUnsignedInt(dbVersion);
				if(bytes)
				{
					bytes.compress(CompressionAlgorithm.LZMA);
					data.writeBytes(bytes);
					FileUtil.save(zipFile,data);
					var file:File = File.applicationDirectory.resolvePath(zipFile);
					file.addEventListener(Event.COMPLETE,onUploadComp);
					file.addEventListener(IOErrorEvent.IO_ERROR,onIoError);
					file.upload(new URLRequest(syncServer));
				}
			}
			
			protected function onUploadComp(event:Event):void
			{
				nativeApplication.exit();
			}
			
			private var qqMusic:QQMusic = new QQMusic;
			
			private var player:Player;
			
			private var failTimes:int = 0;
			
			private var currentSong:Object;
			
			private var isPlaying:Boolean = true;
			/**
			 * 播放下一首
			 */	
			private function playNext():void
			{
				if(!player)
				{
					player = new Player();
					player.addEventListener(PlayEvent.PLAY_COMPLETE,onPlayComp);
					player.addEventListener(PlayEvent.PLAY_PROGRESS,onPlayProgress);
					player.addEventListener(PlayEvent.PLAY_ERROR,onPlayError);
				}
				var song:Object = getSongByRandom();
				if(song&&song.url)
				{
					var url:String = getPortableUrl(song.url);
					if(!url)
					{
						isPlaying = false;
						failTimes++;
						if(failTimes<10)
						{
							playNext();
						}
						return;
					}
					currentSong = song;
					failTimes = 0;
					isPlaying = true;
					countAdded = false;
					player.playSong(url);
					var file:File = File.applicationDirectory.resolvePath(getCoverPath(song));
					if(file.exists)
						cover.source = file.nativePath;
					else
					{
						qqMusic.searchCover(song.artist,song.title,onCoverUrlComp);
						cover.source = null;
					}
					favorBtn.selected = (song.favor=="1");
					titleLabel.text = song.title+" - "+song.artist;
					title = song.title+" - DomFM";
					timeLabel.text = "00:00";
				}
				else
				{
					isPlaying = false;
				}
			}
			
			private var curlLoader:CurlLoader;
			private function onCoverUrlComp(url:String):void
			{
				if(!url)
					return;
				if(!curlLoader)
				{
					curlLoader = new CurlLoader();
					curlLoader.addEventListener(Event.COMPLETE,onCoverLoaded)
				}
				curlLoader.load(url);
			}
			
			private function onCoverLoaded(event:Event):void
			{
				cover.source = curlLoader.loadedBytes;
				if(curlLoader.loadedBytes.length>1024)
					FileUtil.save(getCoverPath(currentSong),curlLoader.loadedBytes);
			}
			
			private function getCoverPath(song:Object):String
			{
				var path:String = "";
				if(song.artist)
					path += song.artist;
				if(song.album)
					path += " - "+song.album;
				path = "covers/"+path+".jpg";
				return path;
			}
			
			private var driverLetters:Array = ["C","D","E","F","G","H","I","J","K","L"];
			
			private function getPortableUrl(url:String):String
			{
				try
				{
					if(new File(url).exists)
						return url;
				}
				catch(e:Error){}
				
				url = url.substring(1);
				for each(var letter:String in driverLetters)
				{
					if(new File(letter+url).exists)
						return letter+url;
				}
				return "";
			}
			/**
			 * 获取一条音乐记录
			 */	
			private function getSongByRandom():Object
			{
				var result:Array;
				if(playILike)
					result= sql.execute("select  Min(count) as count From  song where favor=1");
				else
					result= sql.execute("select  Min(count) as count From  song");
				var minCount:Number = Number(result[0].count);
				if(isNaN(minCount))
					minCount = 0;
				if(playILike)
					result = sql.execute("select * from song where count="+minCount+" and favor=1 order by random() limit 1;");
				else
					result = sql.execute("select * from song where count="+minCount+" and favor<>-1 order by random() limit 1;");
				if(result)
					return result[0];
				return null;
			}
			
			private var countAdded:Boolean = false;
			/**
			 * 播放进度
			 */	
			private function onPlayProgress(event:PlayEvent):void
			{
				if(!countAdded&&event.playedTime>60000)
				{
					sql.execute("update song set count = count+1 where url='"+SQL.escape(event.url)+"';");
					countAdded = true;
				}
				timeLabel.text = StringUtil.toTimeString(event.playedTime);
			}
			/**
			 * 一首歌播放完成
			 */	
			private function onPlayComp(event:PlayEvent):void
			{
				if(!countAdded)
				{
					sql.execute("update song set count = count+1 where url='"+SQL.escape(event.url)+"';");
					countAdded = true;
				}
				playNext();
			}
			/**
			 * 一首歌播放失败
			 */	
			private function onPlayError(event:PlayEvent):void
			{
				playNext();
			}
			/**
			 * 跳过按钮点击
			 */	
			protected function skipBT_clickHandler(event:MouseEvent=null):void
			{
				sql.execute("update song set skip = skip+1 where url='"+SQL.escape(currentSong.url)+"';");
				playNext();
			}
			/**
			 * id3解析器
			 */	
			private var id3Parser:ID3Parser;
			
			/**
			 * 拖拽文件进入
			 */
			private function windowedapplication1_nativeDragEnterHandler(event:NativeDragEvent):void
			{
				if(event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT))
				{       
					if(event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT))
						DragManager.acceptDragDrop(this);    
				}
			}
			/**
			 * 拖拽文件放下
			 */
			private function windowedapplication1_nativeDragDropHandler(event:NativeDragEvent):void
			{
				var arr:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				var list:Array = [];
				for each(var file:File in arr)
				{
					if(file.isDirectory)
					{
						list = list.concat(FileUtil.search(file.nativePath,null,filterFunc));
					}
					else if(filterFunc(file))
					{
						list.push(file);
					}
				}
				var urlList:Array = [];
				for each(file in list)
				{
					urlList.push(file.nativePath);
				}
				id3Parser.parse(urlList);
			}
			/**
			 * 添加按钮点击
			 */	
			protected function addBtn_clickHandler(event:Event):void
			{
				FileUtil.browseForOpen(function(file:File):void{
					var list:Array = FileUtil.search(file.nativePath,null,filterFunc);
					var urlList:Array = [];
					for each(var file:File in list)
					{
						urlList.push(file.nativePath);
					}
					
					id3Parser.parse(urlList);
				},3);
			}
			
			private var sql:SQL = new SQL("music.db");
			/**
			 * 插入一条音乐记录到数据库
			 */	
			private function onGetMusicInfo(event:ID3Event):void
			{
				var result:Array = sql.execute("select title from song where url='"+SQL.escape(event.url)+"'");
				if(result&&result.length>0)
					return;
				var id3:ID3Tag = event.tagInfo;
				if(!id3.title)
				{
					id3.title = FileUtil.getFileName(event.url);
				}
				var type:String = FileUtil.getExtension(event.url);
				sql.execute("insert into song (title,artist,album,url,type,favor,count,skip) values('"+
					SQL.escape(id3.title)+"','"+SQL.escape(id3.artist)+"','"+SQL.escape(id3.album)+"','"+SQL.escape(event.url)+"','"+
					SQL.escape(type)+"',0,0,0);");
				if(!isPlaying)
					playNext();
			}
			
			/**
			 * 文件过滤回调函数
			 */	
			private function filterFunc(file:File):Boolean
			{
				if(file.isDirectory)
					return true;
				if(file.extension==null)
					return false;
				var extension:String = file.extension.toLowerCase();
				if(extension=="mp3"/*||extension=="m4a"||extension=="wma"*/)
					return true;
				return false;
			}
			
			protected function windowedapplication1_windowCompleteHandler(event:AIREvent):void
			{
				var windowX:Number = SharedObjectTool.read(applicationID,"windowX");
				var windowY:Number = SharedObjectTool.read(applicationID,"windowY");
				playILike = SharedObjectTool.read(applicationID,"playILike");
				channelBtn.selected = playILike;
				channelLabel.text = playILike?"我喜欢的":"所有音乐";
				if(isNaN(windowX))
					windowX = (Capabilities.screenResolutionX - width)*0.5;
				if(isNaN(windowY))
					windowY = (Capabilities.screenResolutionY - height)*0.5;
				if(windowX+width>Capabilities.screenResolutionX)
					windowX = Capabilities.screenResolutionX-width;
				if(windowY+height>Capabilities.screenResolutionY)
					windowY = Capabilities.screenResolutionY-height;
				nativeWindow.x = windowX;
				nativeWindow.y = windowY;
				addEventListener(FlexNativeWindowBoundsEvent.WINDOW_MOVE,windowedapplication1_windowMoveHandler);
				var menu:NativeMenu = new NativeMenu();
				var menuItem:NativeMenuItem = new NativeMenuItem("添加音乐");
				menuItem.addEventListener(Event.SELECT,addBtn_clickHandler);
				menu.addItem(menuItem);
				contextMenu = menu;
				playNext();
			}
			
			protected function windowedapplication1_windowMoveHandler(event:FlexNativeWindowBoundsEvent):void
			{
				SharedObjectTool.write(applicationID,"windowX",nativeWindow.x);
				SharedObjectTool.write(applicationID,"windowY",nativeWindow.y);
			}
			
			protected function onKeyDown(event:KeyboardEvent):void
			{
				if(!initialized)
					return;
				timeLabel.text += event.keyCode+" ";
				if(event.ctrlKey&&event.altKey)
				{
					switch(event.keyCode)
					{
						case Keyboard.RIGHT:
							skipBT_clickHandler();
							break;
						case Keyboard.LEFT:
							favorBtn.selected = true;
							likeBT_changeHandler();
							break;
						case Keyboard.F11:
							favorBtn.selected = false;
							likeBT_changeHandler();
							break;
						case Keyboard.F12:
							banBT_clickHandler();
							break;
						case Keyboard.F5:
							pauseMask.visible = !pauseMask.visible;
							if(pauseMask.visible)
							{
								player.pause();
							}
							else
							{
								player.resume();
							}
							break;
					}
				}
			}
			
			private function likeBT_changeHandler(event:Event=null):void
			{
				var favor:String = favorBtn.selected?"1":"0";
				sql.execute("update song set favor = "+favor+" where url='"+SQL.escape(currentSong.url)+"';");
			}
			
			protected function banBT_clickHandler(event:MouseEvent=null):void
			{
				sql.execute("update song set favor = -1 where url='"+SQL.escape(currentSong.url)+"';");
				playNext();
			}
			
			
			
			protected function pauseMask_clickHandler(event:MouseEvent=null):void
			{
				pauseMask.visible = false;
				if(player)
				{
					player.resume();
				}
			}
			
			protected function group1_clickHandler(event:MouseEvent=null):void
			{
				pauseMask.visible = true;
				if(player)
				{
					player.pause();
				}
			}
			
			private var playILike:Boolean = false;
			
			protected function togglebutton1_changeHandler(event:Event):void
			{
				playILike = channelBtn.selected;
				channelLabel.text = playILike?"我喜欢的":"所有音乐";
				SharedObjectTool.write(applicationID,"playILike",playILike);
				playNext();
			}
			
			
		]]>
			
	</fx:Script>
	<fx:Declarations>
	</fx:Declarations>
	<s:BitmapImage x="32" y="30" width="120" height="120" source="@Embed('com/domlib/domFM/skins/assets/coverBg.png')"/>
	<s:Image id="cover" x="32" y="30" width="120" height="120" scaleMode="zoom"
			 skinClass="com.domlib.domFM.skins.ImageSkin"/>
	<s:Group x="180" y="55" width="215" height="45" toolTip="暂停" useHandCursor="true" buttonMode="true" click="group1_clickHandler(event)">
		<s:BitmapImage source="@Embed('com/domlib/domFM/skins/assets/bg.png')"/>
		<s:Label id="titleLabel" showTruncationTip="true" maxDisplayedLines="1" x="8" y="27" width="200" color="#666666" fontWeight="normal"/>
		<s:Label id="timeLabel" x="8" y="5" color="#666666" fontWeight="bold" text="00:00"/>
		<s:Label id="channelLabel" x="162" y="7" color="#FFFFFF" text="所有音乐"/>
	</s:Group>
	
	<s:ToggleButton useHandCursor="true" buttonMode="true" toolTip="我喜欢" id="favorBtn" x="254" y="123" width="40" height="40"
					change="likeBT_changeHandler(event)"
					skinClass="com.domlib.domFM.skins.FavorButtonSkin"/>
	<s:Button useHandCursor="true" buttonMode="true" toolTip="跳过" id="skipBT" x="356" y="123" width="40" height="40"
			  click="skipBT_clickHandler(event)" skinClass="com.domlib.domFM.skins.NextButtonSkin"/>
	<s:Button useHandCursor="true" buttonMode="true" id="banBT" toolTip="不再播放" x="305" y="123" width="40" height="40"
			  click="banBT_clickHandler(event)" skinClass="com.domlib.domFM.skins.BanButtonSkin"/>
	<s:Group id="pauseMask" useHandCursor="true" mouseChildren="false" buttonMode="true" width="100%" height="100%" visible="false" click="pauseMask_clickHandler(event)">
		<s:Rect width="100%" height="100%">
			<s:fill>
				<s:SolidColor color="0xFFFFFF" alpha="0.6"/>
			</s:fill>
		</s:Rect>
		<s:Rect width="100" height="35" radiusX="4" radiusY="4" horizontalCenter="0" verticalCenter="0">
			<s:fill>
				<s:SolidColor color="0x808080"/>
			</s:fill>
		</s:Rect>
		<s:Label color="#FFFFFF" horizontalCenter="0" text="继续收听" verticalCenter="0"/>
	</s:Group>
	<s:ToggleButton id="channelBtn" toolTip="切换列表" useHandCursor="true" buttonMode="true" change="togglebutton1_changeHandler(event)" x="361" y="22" skinClass="com.domlib.domFM.skins.ChangeButtonSkin"/>
</s:WindowedApplication>
